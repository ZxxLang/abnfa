; ABNFA Definition of ABNF

; fork RFC 7405

rulelist       =  1*( rule-Rule--post / (*cwsp cnl) )
                ; 1*( rule-Rule--post / comment-Comment-comments- )

rule           =  rulename definedas elements cnl
                    ; continues if next line starts
                    ;  with white space

rulename       =  ALPHA *(ALPHA / DIGIT / "-")

defInc         =  "=" / "=/"

definedas      =  *cwsp defInc--kind *cwsp
                    ; basic rules definition and
                    ;  incremental alternatives

elements       =  alternation-Choice-alts- *cwsp

cwsp           =  WSP / (cnl WSP)

cnl            =  commentnl / CRLF
                    ; rewrite

commentnl      =  comment-Comment-comments- CRLF
                    ; rewrite

comment        =  ";" *(WSP / VCHAR)
                    ; rewrite

alternation    =  concatenation-Serial-seqs-
               *(*cwsp "/" *cwsp concatenation-Serial-seqs-)

concatenation  =  repetition *(1*cwsp repetition)

repetition     =  option / ([repeat-Repeat-rep] element-rule)

repeat         =  1*DIGIT / (*DIGIT "*" *DIGIT)

element        =  rulename--ref / group /
               charLit-node / num / proseLit-node

group          =  "(" *cwsp alternation-Choice-alts- *cwsp ")"

option         =  "[" *cwsp alternation-Choice-alts- *cwsp "]"

num            =  ("%b" bin-node-text) /
                ("%d" dec-node-text) /
                ("%x" hex-node-text)
                ; rewrite for abnfa

bin            =  1*BIT
               [ 1*("." 1*BIT) / ("-" 1*BIT) ]
                    ; series of concatenated bit values
                    ;  or single ONEOF range

dec            =  1*DIGIT
               [ 1*("." 1*DIGIT) / ("-" 1*DIGIT) ]

hex            =  1*HEXDIG
               [ 1*("." 1*HEXDIG) / ("-" 1*HEXDIG) ]

proseLit       =  "<" prose-text  ">"
                    ; bracketed string of SP and VCHAR
                    ;  without angles
                    ; prose description, to be used as
                    ;  last resort

prose          = *(%x20-3D / %x3F-7E)

; fork RFC 7405

charLal        =  insensitiveLit / sensitiveLit

insensitiveLit  = insensitive-insensitive-true quotedLit
                    ; rewrite for abnfa

insensitive     = "%i"

sensitiveLit    = [ sensitive-insensitive-false ] quotedLit
                        ; quoted-string is case-sensitive-string
                        ; rewrite for abnfa

sensitive       = "%s"

quotedLit       =  DQUOTE string-text  DQUOTE
                    ; quoted string of SP and VCHAR
                    ;  without DQUOTE

string          = *(%x20-21 / %x23-7E)

; Core Rules

CRLF    =  (%x0D %x0A) / %x0D / %x0A

ALPHA   =  %x41-5A / %x61-7A   ; A-Z / a-z

BIT     =  %x30-31

CHAR    =  %x01-7F

; CTL     =  %x00-1F / %x7F

DIGIT   =  BIT / %x32-39 ; 0-9

DQUOTE  =  %x22    ; " (Double Quote)

HEXDIG  =  DIGIT / %x41-46 / %x61-66 ; 0-9A-Fa-f

SP      =  %x20

HTAB    =  %x09

; OCTET   =  %x00-FF ; 8 bits of data

WSP     =  SP / HTAB

VCHAR   =  %x21-7E
