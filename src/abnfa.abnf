; ABNFA Definition of ABNF

; fork RFC 7405

ast            =  1*( rule-Rule-fetch-name-defs / (*cwsp cnl) )

name           =  ALPHA *(ALPHA / DIGIT)

rule           =  name--to-name definedAs elements cnl

defInc         =  "=/"

definedAs      =  *cwsp ("=" / defInc--alter-flag-list) *cwsp

elements       =  alternation-Choice-to-alts-list *cwsp

cwsp           =  WSP / (cnl WSP)

cnl            =  [comment-Comment-to-comments-list] CRLF

comment        =  commentLit--term

commentLit     =  ";" *(WSP / VCHAR)

alternation    =  concatenation-Serial-mix-seqs-list
               *(*cwsp "/" *cwsp concatenation-Serial-mix-seqs-list)

concatenation  =  repetition *(1*cwsp repetition)

repetition     =  option--push-seqs / ([repeat-Repeat-rep] element--push-seqs)

repeat         =  1*DIGIT / (*DIGIT "*" *DIGIT)

element        =  ref-Ref / group /
                  charLit-Text / binary-Binary /
                  range-Range / proseLit-Text

group          =  "(" *cwsp alternation-Choice *cwsp ")"

option         =  "[" *cwsp alternation-Choice *cwsp "]"

ref            =  name---ref ["-" action-Action-to-action]

action         =  name--to-produce [
                  "-" [name--to-method] [
                  "-" [name--to-key] [
                  "-" [name--to-flag]
                  ]]]

b              = "b"

d              = "d"

x              = "x"


binary         = "%" (  b--to-formal bin /
                        d--to-formal dec /
                        x--to-formal hex )

range          = "%" (  b--to-formal binRange /
                        d--to-formal decRange /
                        x--to-formal hexRange )

binLit         =  1*BIT

decLit         =  1*DIGIT

hexLit         =  1*HEXDIG

bin            =  binLit--to-seqs-list [ 1*("." binLit--to-seqs-list) ]

dec            =  decLit--to-seqs-list [ 1*("." decLit--to-seqs-list) ]

hex            =  hexLit--to-seqs-list [ 1*("." hexLit--to-seqs-list) ]

binRange       =  binLit--to-first [ ("-" binLit--to-last) ]

decRange       =  decLit--to-first [ ("-" decLit--to-last) ]

hexRange       =  hexLit--to-first [ ("-" hexLit--to-last) ]

proseLit       =  "<" prose--raw  ">"

prose          = *(%x20-3D / %x3F-7E)

; fork RFC 7405

charLit         =  insensitiveLit / sensitiveLit

insensitiveLit  = "%" insensitive--to-formal quotedLit

insensitive     = "i"

sensitiveLit    = [ "%" sensitive--to-formal ] quotedLit

sensitive       = "s"

quotedLit       =  DQUOTE string--to-raw  DQUOTE

string          = *(%x20-21 / %x23-7E)

; Core Rules

CRLF    =  (%x0D %x0A) / %x0D / %x0A

ALPHA   =  %x41-5A / %x61-7A   ; A-Z / a-z

BIT     =  %x30-31

; CHAR    =  %x01-7F

; CTL     =  %x00-1F / %x7F

DIGIT   =  %x30-39 ; 0-9

DQUOTE  =  %x22    ; " (Double Quote)

HEXDIG  =  DIGIT / %x41-46 / %x61-66 ; 0-9A-Fa-f

SP      =  %x20

HTAB    =  %x09

; OCTET   =  %x00-FF ; 8 bits of data

WSP     =  SP / HTAB

VCHAR   =  %x21-7E
