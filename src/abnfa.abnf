; ABNFA Definition of ABNF

; fork RFC 7405

rulelist       =  1*( rule-Rule--push / (*cwsp cnl) )
                ; 1*( rule-Rule--push / comment-Comment-comments- )

rule           =  rulename--name definedas elements cnl
                    ; continues if next line starts
                    ;  with white space

rulename       =  ALPHA *(ALPHA / DIGIT)

refname        =  ALPHA *(ALPHA / DIGIT / "-")

defInc         =  "=" / "=/"

definedas      =  *cwsp defInc---kind *cwsp
                    ; basic rules definition and
                    ;  incremental alternatives

elements       =  alternation-Choice--push *cwsp

cwsp           =  WSP / (cnl WSP)

cnl            =  commentnl / CRLF
                    ; rewrite

commentnl      =  comment-Comment-comments- CRLF
                    ; rewrite

comment        =  ";" commentLit--raw

commentLit     =  *(WSP / VCHAR)

alternation    =  concatenation-Serial--push
               *(*cwsp "/" *cwsp concatenation-Serial--push)


concatenation  =  repetition *(1*cwsp repetition)

repetition     =  option-Choice / ([repeat-Repeat-rep] element)

repeat         =  1*DIGIT / (*DIGIT "*" *DIGIT)

element        =  refname-Ref / group-Choice /
               charLit-Text / binary-Binary / range-Range / proseLit-Text

group          =  "(" *cwsp alternation---push *cwsp ")"

option         =  "[" *cwsp alternation---push *cwsp "]"

b              = "b"

d              = "d"

x              = "x"


binary         = "%" (b--formal bin / d--formal dec / x--formal hex)

range          = "%" (b--formal binRange / d--formal decRange / x--formal hexRange)

binLit         =  1*BIT

decLit         =  1*DIGIT

hexLit         =  1*HEXDIG

bin            =  binLit--seqs- [ 1*("." binLit--seqs-) ]

dec            =  decLit--seqs- [ 1*("." decLit--seqs-) ]

hex            =  hexLit--seqs- [ 1*("." hexLit--seqs-) ]

binRange       =  binLit--first [ ("-" binLit--last) ]

decRange       =  decLit--first [ ("-" decLit--last) ]

hexRange       =  hexLit--first [ ("-" hexLit--last) ]

proseLit       =  "<" prose--raw  ">"
                    ; bracketed string of SP and VCHAR
                    ;  without angles
                    ; prose description, to be used as
                    ;  last resort

prose          = *(%x20-3D / %x3F-7E)

; fork RFC 7405

charLit         =  insensitiveLit / sensitiveLit

insensitiveLit  = "%" insensitive--formal quotedLit
                    ; rewrite for abnfa

insensitive     = "i"

sensitiveLit    = [ "%" sensitive--formal ] quotedLit
                        ; quoted-string is case-sensitive-string
                        ; rewrite for abnfa

sensitive       = "s"

quotedLit       =  DQUOTE string--raw  DQUOTE
                    ; quoted string of SP and VCHAR
                    ;  without DQUOTE

string          = *(%x20-21 / %x23-7E)

; Core Rules

CRLF    =  (%x0D %x0A) / %x0D / %x0A

ALPHA   =  %x41-5A / %x61-7A   ; A-Z / a-z

BIT     =  %x30-31

; CHAR    =  %x01-7F

; CTL     =  %x00-1F / %x7F

DIGIT   =  BIT / %x32-39 ; 0-9

DQUOTE  =  %x22    ; " (Double Quote)

HEXDIG  =  DIGIT / %x41-46 / %x61-66 ; 0-9A-Fa-f

SP      =  %x20

HTAB    =  %x09

; OCTET   =  %x00-FF ; 8 bits of data

WSP     =  SP / HTAB

VCHAR   =  %x21-7E
